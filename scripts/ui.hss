# ui.hss

script, updateHud, begin
    variable(sl)

    # HP
    variable(hpBar)
    hpBar := lookupSlice(sli:hpBar, hud)
    setSliceWidth(hpBar, multdiv(sliceWidth(firstChild(hpBar)), getHeroStat(me, stat:HP), getHeroStat(me, stat:HP, maximumStat)))
    clearString(0), appendNumber(0, max(getHeroStat(me, stat:hp), 0))
    setSliceText(lookupSlice(sli:hpCurrent, hud), 0)
    clearString(0), appendNumber(0, getHeroStat(me, stat:hp, maximumStat))
    setSliceText(lookupSlice(sli:hpMax, hud), 0)
    # stamina
    variable(staminaBar)
    staminaBar := lookupSlice(sli:stamina, hud)
    setSpriteFrame(staminaBar, getHeroStat(me, stat:SP))
    # ammo
    variable(ammoBar)
    ammoBar := lookupSlice(sli:ammo, hud)
    setSpriteFrame(ammoBar, getHeroStat(me, stat:Hits))
    # Boss hp bar
    bossBarTick()
    # Money
    clearString(0)
    appendNumber(0, partyMoney)
    setSliceText(lookupSlice(sli:money, hud), 0)
    # Menu hud
    variable(bg)
    bg := lookupSlice(sli:menuHud, hud)
    if(findMenuID(menu:main) || findMenuID(menu:shop)) then(
        setSliceVisible(bg, true)
    ) else(
        setSliceVisible(bg, false)
    )
    if(getSliceVisible(bg)) then(
        clearString(0), appendNumber(0, getHeroStat(me, stat:Atk))
        setSliceText(lookupSlice(sli:melee, bg), 0)
        clearString(0), appendNumber(0, getHeroStat(me, stat:Magic))
        setSliceText(lookupSlice(sli:ranged, bg), 0)
        clearString(0), appendNumber(0, getHeroStat(me, stat:Acc))
        setSliceText(lookupSlice(sli:acc, bg), 0)
        clearString(0), appendNumber(0, playerSpeed/100)
        appendAscii(0, asciiFromString(0, stringLength(0)))
        replaceChar(0, 2, 46)
        setSliceText(lookupSlice(sli:speed, bg), 0)
        clearString(0), appendNumber(0, getHeroStat(me, stat:MP~))
        setSliceText(lookupSlice(sli:spRecover, bg), 0)
        clearString(0), appendNumber(0, getHeroStat(me, stat:Ctr))
        setSliceText(lookupSlice(sli:reload, bg), 0)
    )
end

script, toggleCutsceneBars, state=false, begin
    variable(parent, child)

    parent := lookupSlice(sli:cutsceneBars, hud)
    setSliceExtra(parent, extra0, state)
    setSliceExtra(parent, extra1, 0)

    setTimer(timer:cutsceneBars, 0, 1, @cutsceneBarTick)
end

script, cutsceneBarTick, begin
    variable(parent, child, val)

    parent := lookupSlice(sli:cutsceneBars, hud)
    if(getSliceExtra(parent, extra0)) then(
        val := 1
    ) else(
        val := -1
    )

    setSliceExtra(parent, extra1, getSliceExtra(parent, extra1)+val)

    child := firstChild(parent)
    while(child) do(
        setSliceHeight(child, sliceHeight(child)+val)
        child := nextSibling(child)
    )

    if(getSliceExtra(parent, extra1) < 40) then(
        setTimer(timer:cutsceneBars, 0, 1, @cutsceneBarTick)
    )
end

script, toggleBossBar, state=false, name=-1, begin
    variable(sl, bar, text)

    sl := lookupSlice(sli:bossBar, hud)
    setSliceVisible(sl, state)

    bar := firstRectChild(firstRectChild(sl))
    setSliceWidth(bar, sliceWidth(sliceParent(bar))--2)

    if(name > -1) then(
        text := firstTextChild(sl)
        setSliceText(text, name)
    )
end

script, bossBarTick, begin
    variable(sl, bar, npcSl)

    sl := lookupSlice(sli:bossBar, hud)

    if(getSliceVisible(sl) == false) then(exitScript())

    if(globalNPCReference(currentBoss, 0) == false) then(
        currentBoss := 0
        toggleBossBar(false)
        exitScript()
    )

    npcSl := getNPCSprite(globalNPCReference(currentBoss, 0))

    bar := firstRectChild(firstRectChild(sl))
    setSliceWidth(bar, multdiv(sliceWidth(sliceParent(bar))--2, getSliceExtra(npcSl, extra2), getEnemyHP(globalNPCReference(currentBoss, 0))))
end 

script, setMessage, str, begin
    variable(sl)

    sl := lookupSlice(sli:msg, hud)
    setSliceText(firstChild(sl), str)
    setSliceVisible(sl, true)

    setTimer(timer:message, 60, 1, @clearMessage)
end

script, clearMessage, begin
    variable(sl)

    sl := lookupSlice(sli:msg, hud)
    clearString(0)
    setSliceText(firstChild(sl), 0)
    setSliceVisible(sl, false)
end

plotscript, selectDifficulty, begin
    variable(menu, col, text)

    menu := openMenu(menu:difficulty)
    col := loadSliceCollection(5)
    text := lookupSlice(sli:diffText, col)

    if(difficulty == -1) then(
        selectMenuItem(menuItemBySlot(menu, 1))
    ) else(
        selectMenuItem(menuItemBySlot(menu, difficulty))
    )

    while(menuIsOpen(menu)) do(
        clearString(0)
        switch(menuItemSlot(selectedMenuItem(menu))) do(
            case(0) $0="- Reduced damage from enemies"
            case(1) $0="- Normal damage from enemies"
            case(2) $0="- Increased damage from enemies\n- No healing items"
        )
        setSliceText(text, 0)
        wait
    )

    freeSlice(col)
end

plotscript, difficultyMenuItem, handle, begin
    difficulty := menuItemSlot(handle)
    closeMenu(topMenu)
end