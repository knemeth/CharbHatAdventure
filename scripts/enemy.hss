# enemy.hss

# NPC EXTRAS
# extra0 = Action delay
# extra1 = Whether the npc was hit by the player's current melee attack
# extra2 = stun duration

defineConstant, begin
    0,enemy:dummy
    1,enemy:melee
    2,enemy:ranged
    3,enemy:testboss
    4,enemy:charvile
    5,enemy:slime
    6,enemy:chadbile
    7,enemy:fan
    8,enemy:flame
    9,enemy:starbile
    10,enemy:speaker
    11,enemy:stardummy
    30,enemy:actDelay

    0,ai:idle
    1,ai:spray 
    2,ai:aoe
    3,ai:melee
    4,ai:mega
    5,ai:wait
    6,ai:slimetime
    7,ai:spin 
    8,ai:charge
end

script, initEnemies, begin
    # loop through npcs and apply default values
    variable(ref)

    ref := nextNPCReference()
    while(ref) do(
        if(getNPCPool(ref) == pool:global) then(
            setSliceExtra(getNPCSprite(ref), extra2, getEnemyHP(ref))
            setEnemyState(ref, ai:wait)
        )
        
        ref := nextNPCReference(ref)
    )
end

script, createEnemy, id, x, y, begin
    variable(ref)

    ref := createGlobalNPC(id, x, y)
    initObject(getNPCSlice(ref), getEnemyWidth(ref), getEnemyHeight(ref), 0, 0)
    setSliceExtra(getNPCSprite(ref), extra2, getEnemyHP(ref))

    if(id == enemy:fan) then(
        setEnemyPatternStep(ref, random(3, 4))
    ) elseif(id == enemy:flame) then(
        setEnemyPatternStep(ref, random(0, 240))
        dissolveSprite(getNPCSprite(ref), dissolve:fade, 600)
    )

    exitReturning(ref)
end

script, enemyEachtick, begin
    # loop through npcs and apply behaviors based on id
    variable(ref)

    ref := nextNPCReference()
    while(ref) do(
        if(getNPCPool(ref) == pool:global) then(
            enemyBehavior(ref)
        ) else(
            if(getEnemyActDelay(ref) > 0) then(setEnemyActDelay(ref, getEnemyActDelay(ref) -- 1))
        )
        ref := nextNPCReference(ref)
    )
end

script, enemyBehavior, ref, begin
    # check enemy type and apply behavior based on current conditions
    variable(dist, seq, npcSl, bearing, subX, subY, i, spawn, x, y)

    npcSl := getNPCSlice(ref)

    if(getEnemyStun(ref) > 0) then(
        stunEnemy(ref, getEnemyStun(ref)--1)
        exitScript()
    )

    if(getAttackDelay(npcSl) > 0) then(
        setAttackDelay(npcSl, getAttackDelay(npcSl)--1)
    )

    if(getEnemyActDelay(ref) > 0) then(
        setEnemyActDelay(ref, getEnemyActDelay(ref)--1)
        # Move if still moving
        if(getSliceV(npcSl) > 0 && getSliceVisible(npcSl)) then(
            bearing := getSliceDir(npcSl)
            subX := getXSubpixels(getSliceV(npcSl), bearing)
            subY := getYSubpixels(getSliceV(npcSl), bearing)
            applyMove(npcSl, subX, subY)
            # Direction
            if(bearing > 315) then(setNPCDirection(ref, up))
            elseif(bearing > 225) then(setNPCDirection(ref, left))
            elseif(bearing > 135) then(setNPCDirection(ref, down))
            elseif(bearing > 45) then(setNPCDirection(ref, right))
            else(setNPCDirection(ref, up))
        )
        exitScript()
    )

    switch(getNPCID(ref)) do(
        case(enemy:dummy, enemy:stardummy) do(
            # Do nothing
        )
        case(enemy:melee) do(
            # If player within certain radius, move towards them
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist <= getEnemyAttackHeight(ref)) then(
                # Attack
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, 0)
            ) elseif(dist / 20 < 6 && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Move towards player
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            )
        )
        case(enemy:ranged) do(
            # If player within certain radius, move towards them
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist / 20 < 4  && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Run away
                bearing := getBearing(sliceCX(player), sliceCY(player), sliceCX(npcSl), sliceCY(npcSl))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) elseif(dist / 20 < 6 && dist / 20 > 5 && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Move towards player
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) 

            if(dist / 20 < 7 && getAttackDelay(npcSl) == 0) then(
                # Fire projectile
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, bearing, projectile:speed, false)
                setAttackDelay(npcSl, enemy:actDelay * 3)
            )
        )
        case(enemy:testboss) do(
            # when player gets in range, start fucking shit up
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist / 20 < 8) then(
                switch(getEnemyState(ref)) do(
                    case(ai:idle) setEnemyActDelay(ref, enemy:actDelay), setEnemyState(ref, random(1, 4))
                    case(ai:spray) do(
                        if(getEnemyPatternStep(ref) < 16) then(
                            createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        
                        setEnemyActDelay(ref, 12)
                    )
                    case(ai:aoe) do(
                        if(getEnemyPatternStep(ref) < 8) then(
                            startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        setEnemyActDelay(ref, 24)
                    )
                    case(ai:mega) do(
                        if(getEnemyPatternStep(ref) < 32) then(
                            createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                            if(getEnemyPatternStep(ref),mod,4) then(
                                for(i, 0, 7) do(
                                    createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, i*(360/8), multdiv(projectile:speed, 3, 5), false)
                                )
                            ) elseif(getEnemyPatternStep(ref),mod,6) then(
                                startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                            )
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        setEnemyActDelay(ref, 8)
                    )
                    case(ai:melee) do(
                        startAOE(sliceCX(npcSl)--sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                        startAOE(sliceCX(npcSl)+sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                        startAOE(0, sliceCY(npcSl)--sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                        startAOE(0, sliceCY(npcSl)+sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                        setEnemyActDelay(ref, 24)
                        setEnemyState(ref, ai:idle)
                    )
                )
            )
        )
        case(enemy:slime) do(
            # If player within certain radius, move towards them
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist <= 64) then(
                # Move towards player
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) else(
                # Move in random direction
                bearing := random(0, 359)
                setEnemyActDelay(ref, random(16, 64))
                setSliceDir(npcSl, bearing)
                if(random(1,10) <= 2) then(
                    setSliceV(npcSl, 0)
                ) else(
                    setSliceV(npcSl, getEnemySpeed(ref))
                )
            )
        )
        case(enemy:charvile) do(
            if(getEnemyState(ref) == ai:wait) then(break)   # fight hasn't started yet

            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(getDistance(sliceCX(npcSl), sliceCY(npcSl), 84*20, 78*20) / 20 > 6) then(
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), 84*20, 78*20)
                setSliceDir(npcSl, bearing)
            ) elseif(dist / 20 > 7) then(
                # move toward player
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) else(
                setSliceV(npcSl, 0)
            )
            
            switch(getEnemyState(ref)) do(
                case(ai:idle) do(
                    if(dist < 58 && readTimer(timer:boss) <= 0) then(
                        setEnemyActDelay(ref, 60)
                        setEnemyState(ref, ai:melee)
                    ) elseif(getEnemySequenceStep(ref) == 0) then(
                        setEnemyActDelay(ref, 50)
                        setEnemySequenceStep(ref, 1)
                        setEnemyState(ref, ai:aoe)
                    ) elseif(getEnemySequenceStep(ref) == 1) then(
                        setEnemyActDelay(ref, 50)
                        setEnemySequenceStep(ref, 2)
                        setEnemyState(ref, ai:spray)
                    ) elseif(getEnemySequenceStep(ref) == 7) then(
                        # spawn lil bois
                        for(i, 0, npcCopyCount(4)--1) do(
                            if(random(1,10) < 6 || minionCount >= 8) then(continue)
                            spawn := createEnemy(enemy:slime, npcX(npcReference(4, i)), npcY(npcReference(4, i)))
                            minionCount += 1
                            dissolveSprite(getNPCSprite(spawn), dissolve:melt, 60, 0, true)
                        )
                        setEnemyActDelay(ref, 120)
                        setEnemySequenceStep(ref, 8)
                    ) elseif(getEnemySequenceStep(ref) == 11) then(
                        setEnemySequenceStep(ref, 0)
                        dissolveSprite(getNPCSprite(ref), dissolve:melt, 120, false)
                        setEnemyActDelay(ref, 120)
                        setEnemyState(ref, ai:slimetime)
                    ) else(
                        setEnemySequenceStep(ref, getEnemySequenceStep(ref)+1)
                        if(getEnemySequenceStep(ref) > 2 && getEnemySequenceStep(ref),mod,2 == 0 && random(1,3) > 1) then(
                            telegraphFX()
                            setEnemyActDelay(ref, 100)
                            setEnemyState(ref, ai:mega)
                        ) else(
                            setEnemyActDelay(ref, 50)
                            setEnemyState(ref, random(1, 2))
                        )
                    )
                )
                case(ai:spray) do(
                    if(getEnemyPatternStep(ref) < 16) then(
                        playSound(sfx:shot, false, true)
                        createProjectile(3, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 12)
                )
                case(ai:aoe) do(
                    if(getEnemyPatternStep(ref) < 8) then(
                        startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 24)
                )
                case(ai:mega) do(
                    if(getEnemyPatternStep(ref) < 32) then(
                        playSound(sfx:shot, false, true)
                        createProjectile(3, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                        if(getEnemyPatternStep(ref),mod,4) then(
                            for(i, 0, 7) do(
                                createProjectile(4, sliceCX(npcSl), sliceCY(npcSl), 10, 10, i*(360/8), multdiv(projectile:speed, 3, 5), false)
                            )
                        ) elseif(getEnemyPatternStep(ref),mod,6) then(
                            startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                        )
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyActDelay(ref, 240)
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 8)
                )
                case(ai:melee) do(
                    startAOE(sliceCX(npcSl)--sliceWidth(npcSl), sliceCY(npcSl), 50, 50, 60, 2, 6, 6)
                    startAOE(sliceCX(npcSl)+sliceWidth(npcSl), sliceCY(npcSl), 50, 50, 60, 2, 6, 6)
                    startAOE(sliceCX(npcSl), sliceCY(npcSl)--sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                    startAOE(sliceCX(npcSl), sliceCY(npcSl)+sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                    setEnemyActDelay(ref, 80)
                    setEnemyState(ref, ai:idle)
                    setTimer(timer:boss, 160, 1)    # don't melee again immediately
                )
                case(ai:slimetime) do(
                    stopTimer(timer:charvileUndulate)
                    dissolveSprite(getNPCSprite(ref), dissolve:melt, 60)
                    setTimer(timer:boss, 60, 1, @setVisibility)
                    setTimerArgs(timer:boss, npcSl, false)
                    for(i, 0, npcCopyCount(4)--1) do(
                        if(getEnemySequenceStep(ref) == 0) then(
                            if(i,mod,2) then(continue)
                        ) elseif(i,mod,2 == false) then(
                            continue
                        )
                        startAOE(sliceCX(getNPCSlice(npcReference(4, i))), sliceCY(getNPCSlice(npcReference(4, i))), 50, 50, 60, 2, 6, 6)
                        if(random(1,10) < 3 && minionCount < 8) then(
                            spawn := createEnemy(enemy:slime, npcX(npcReference(4, i)), npcY(npcReference(4, i)))
                            minionCount += 1
                            dissolveSprite(getNPCSprite(spawn), dissolve:melt, 60, 0, true)
                        )
                    )

                    playSound(sfx:shot, false, true)
                    for(i, 0, 360, 20) do(
                        createProjectile(4, sliceCX(npcSl), sliceCY(npcSl), 10, 10, i + (getEnemySequenceStep(ref)*5), multdiv(projectile:speed, 3, 5), false)
                    )
                    
                    if(getEnemySequenceStep(ref) == 5) then(
                        setSliceVisible(npcSl, true)
                        dissolveSprite(getNPCSprite(ref), dissolve:melt, 60, 1, true)
                        setTimer(timer:boss, 60, 1, @charvileUndulate)
                        setEnemyActDelay(ref, 500)
                        setEnemyState(ref, ai:idle)
                    ) else(
                        setEnemyActDelay(ref, 60)
                        setEnemySequenceStep(ref, getEnemySequenceStep(ref)+1)
                    )
                )
            )
        )
        case(enemy:chadbile) do(
            if(getEnemyState(ref) == ai:wait) then(break)   # fight hasn't started yet
            
            switch(getEnemyState(ref)) do(
                case(ai:idle) do(
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 50) then(
                        newSet(50)
                    )
                    setEnemyMelee(ref, false)
                    for(i, 0, npcCopyCount(12)--1) do(
                        if(getEnemySequenceStep(ref) > 0) then(
                            continue
                        )
                        if(minionCount < 8) then(
                            spawn := createEnemy(enemy:fan, npcX(npcReference(12, i)), npcY(npcReference(12, i)))
                            minionCount += 1
                        )
                    )
                    # put up fences
                    for(x, 12, 25, 13) do(
                        for(y, 33, 34) do(
                            writePassBlock(x, y, northwall+southwall+eastwall+westwall)
                            writeMapBlock(x, y, 133, 3)
                        )
                    )

                    dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                    if(dist / 20 > 4 && getSliceV(npcSl) == 0) then(
                        # move toward player
                        bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                        setSliceDir(npcSl, bearing)
                        setSliceV(npcSl, getEnemySpeed(ref))
                    ) else(
                        setSliceDir(npcSl, -1)
                        setSliceV(npcSl, 0)
                    )

                    seq := getEnemySequenceStep(ref)
                    
                    if(dist < 58 && readTimer(timer:boss) <= 0) then(
                        setEnemyActDelay(ref, 30)
                        setEnemyState(ref, ai:melee)
                    ) elseif(seq == 2 || seq == 3) then(
                        newSet(55)
                        setEnemyActDelay(ref, 20)
                        setEnemyState(ref, ai:charge)
                        setEnemySequenceStep(ref, seq+random(0,1))
                    ) elseif(seq == 4) then(
                        newSet(55)
                        setEnemySequenceStep(ref, 0)
                        setEnemyActDelay(ref, 20)
                        setEnemyState(ref, ai:mega)
                    ) elseif(seq == 6) then(
                        newSet(55)
                        setEnemySequenceStep(ref, seq+1)
                        setEnemyActDelay(ref, 180)
                        setEnemyState(ref, ai:spin)
                    ) elseif(seq == 7) then(
                        setEnemyActDelay(ref, 400)
                        setEnemySequenceStep(ref, 0)
                        setEnemyState(ref, ai:idle)
                    ) else(
                        setEnemyActDelay(ref, 50)
                        setEnemyState(ref, ai:melee)
                        setEnemySequenceStep(ref, getEnemySequenceStep(ref)+1)
                    )
                )
                case(ai:melee) do(
                    # move toward player
                    bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                    setSliceDir(npcSl, bearing)
                    setSliceV(npcSl, getEnemySpeed(ref) / 2)
                    newSet(55)
                    setEnemyActDelay(ref, 30)
                    setEnemyState(ref, ai:spin)
                    setEnemyPatternStep(ref, 58)
                    setTimer(timer:boss, 90, 1)    # don't melee again immediately
                )
                case(ai:spin) do(
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 53) then(
                        newSet(53)
                    )
                    setEnemyMelee(ref, true)
                    playSound(sfx:swish, false, true)
                    # move toward player
                    bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                    setSliceDir(npcSl, bearing)
                    setSliceV(npcSl, getEnemySpeed(ref) + 500)
                    if(getEnemyPatternStep(ref) < 60) then(
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 10)
                )
                case(ai:charge) do(
                    # move toward player
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) == 50) then(
                        newSet(52)
                        setEnemyActDelay(ref, 20)
                        break
                    ) elseif(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 53) then(
                        newSet(53)
                    )
                    setEnemyMelee(ref, true)
                    playSound(sfx:swish, false, true)
                    if(getSliceDir(npcSl) == -1) then(
                        bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                        setSliceDir(npcSl, bearing)
                        setSliceV(npcSl, getEnemySpeed(ref) * 2)
                    )
                    createEnemy(enemy:flame, sliceCX(npcSl)/20, sliceCY(npcSl)/20)
                    if(getEnemyPatternStep(ref) < 5) then(
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                        newSet(50)
                    )
                    setEnemyActDelay(ref, 8)
                )
                case(ai:mega) do(
                    # remove fences
                    for(x, 12, 25, 13) do(
                        for(y, 33, 34) do(
                            writePassBlock(x, y, 0)
                            writeMapBlock(x, y, 0, 3)
                        )
                    )

                    if(getSliceVisible(npcSl) == false) then(
                        # Drop down
                        playSound(sfx:fall, false, true)
                        startAOE(sliceCX(player), sliceCY(player), getEnemyAttackWidth(ref), getEnemyAttackHeight(ref)*2, 28, 3)
                        vfx := loadWalkaboutSprite(55)      # fake chadbile
                        setParent(vfx, lookupSlice(sl:mapLayer4))
                        putSlice(npcSl, sliceCX(player)--(sliceWidth(npcSl)/2), sliceCY(player)--(sliceHeight(npcSl)/2))
                        putSliceSubpixel(npcSl, sliceX(npcSl), sliceY(npcSl))
                        updateNPCorHeroFromSlice(npcSl)
                        putSlice(vfx, sliceCX(npcSl)--(sliceWidth(vfx)/2), sliceCY(npcSl)--(getScreenHeight()/2)--sliceHeight(vfx))
                        
                        createParticle(vfx, 28, 5000, 180, 0, false, dissolve:puff, true)
                        setTimer(timer:boss, 27, 1, @setVisibility)
                        setTimerArgs(timer:boss, npcSl, true)
                    ) elseif(getEnemySequenceStep(ref) < 6) then(
                        # Jump up
                        playSound(sfx:rise, false, true)
                        setSliceVisible(npcSl, false)
                        vfx := loadWalkaboutSprite(55)      # fake chadbile
                        setParent(vfx, lookupSlice(sl:mapLayer4))
                        putSlice(vfx, sliceCX(npcSl)--(sliceWidth(vfx)/2), sliceCY(npcSl)--(sliceHeight(vfx)))
                        createParticle(vfx, 28, 5000, 0, 0, false, dissolve:puff)
                        vfx := loadHeroSprite(15)       # shadow
                        setParent(vfx, lookupSlice(sl:mapLayer4))
                        setHorizAnchor(vfx, edge:center), setVertAnchor(vfx, edge:middle)
                        putSlice(vfx, sliceCX(npcSl), sliceCY(npcSl))
                        createAnimation(vfx, 28, 5, false, dissolve:fade)
                    )
                    
                    if(getEnemySequenceStep(ref) >= 6) then(
                        newSet(50)
                        setEnemyActDelay(ref, 600)
                        setEnemyState(ref, ai:idle)
                    ) else(
                        setEnemyActDelay(ref, 120)
                        setEnemySequenceStep(ref, getEnemySequenceStep(ref)+1)
                    )
                )
            )
        )
        case(enemy:fan) do(
            # speech bubble
            if(getEnemyPatternStep(ref) >= 8) then(
                setEnemyPatternStep(ref, random(0, 2))
                # showBubble
                variable(vfx)
                vfx := loadHeroSprite(14)
                setParent(vfx, npcSl)
                setVertAnchor(vfx, edge:bottom)
                createParticle(vfx, 80, 400, random(270, 360))
            )
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(getNPCSlice(globalNPC(enemy:chadbile))), sliceCY(getNPCSlice(globalNPC(enemy:chadbile))))
            if(dist / 20 > 2  && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Move toward chadbile
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(getNPCSlice(globalNPC(enemy:chadbile))), sliceCY(getNPCSlice(globalNPC(enemy:chadbile))))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
                setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
            ) elseif(random(0,9) == 0) then(
                # Move randomly
                bearing := random(0, 360)
                setEnemyActDelay(ref, random(10, 30))
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
                setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
            )

            if(dist / 20 < 7 && getAttackDelay(npcSl) == 0) then(
                # Fire projectile
                bearing := getBearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, bearing+random(-10,10), projectile:speed, false)
                setAttackDelay(npcSl, random(180, 360))
                setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
            )
        )
        case(enemy:flame) do(
            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
            if(getEnemyPatternStep(ref) > 480) then(
                destroyNPC(ref)
            )
        )
        case(enemy:starbile) do(
            if(getEnemyState(ref) == ai:wait) then(break)   # fight hasn't started yet
            seq := getEnemySequenceStep(ref)
            
            switch(getEnemyState(ref)) do(
                case(ai:idle) do(
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 62) then(
                        newSet(62)
                    )

                    dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                    
                    if(seq == 1 || seq == 3) then(
                        newSet(72)
                        setEnemyActDelay(ref, player:animSpeed * 6)
                        startAOE(sliceCX(npcSl), sliceCY(npcSl), 40, 16, player:animSpeed, 3, 16, 3)
                        setEnemyState(ref, ai:charge)
                        setEnemySequenceStep(ref, seq+1)
                    ) elseif(seq == 2 || seq == 4) then(
                        newSet(72)
                        setEnemySequenceStep(ref, 0)
                        setEnemyActDelay(ref, player:animSpeed * 6)
                        setEnemyState(ref, ai:spray)
                        setEnemySequenceStep(ref, seq+1)
                    ) elseif(seq == 6) then(
                        newSet(72)
                        setEnemySequenceStep(ref, 0)
                        setEnemyActDelay(ref, player:animSpeed * 6)
                        startAOE(sliceCX(npcSl), sliceCY(npcSl), 40, 16, player:animSpeed * 6, 3, 16, 3)
                        setEnemyState(ref, ai:mega)
                    ) else(
                        newSet(72)
                        setEnemyActDelay(ref, player:animSpeed * 6)
                        setEnemyState(ref, ai:aoe)
                        setEnemySequenceStep(ref, seq+1)
                    )
                )
                case(ai:aoe) do(
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 62) then(
                        newSet(62)
                    )
                    if(getEnemyPatternStep(ref) < 16) then(
                        if(getEnemyPatternStep(ref),mod,2 == 0) then(
                            startAOE(sliceCX(player)+random(-16,16), sliceCY(player)+random(-8,8), 40, 16, 45, 3, 16, 3)
                        ) else(
                            variable(boltCount)
                            boltCount := 3
                            if(windDirection <> 0) then(boltCount := 7)
                            for(i, 0, boltCount) do(
                                startAOE(random(158*20, 170*20), random(16*20, 23*20), 40, 16, 45, 3, 16, 3)
                            )
                        )
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, player:animSpeed * 6)
                )
                case(ai:spray) do(
                    if(getSliceExtra(firstChild(getNPCSprite(ref)), extra0) <> 62) then(
                        newSet(62)
                    )
                    if(getEnemyPatternStep(ref) == 0) then(
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        setEnemyActDelay(ref, 48)
                    ) elseif(getEnemyPatternStep(ref) < 5) then(
                        playSound(sfx:shot, false, true)
                        for(i, 0, 3) do(
                            createProjectile(7, sliceCX(npcSl), sliceCY(npcSl), 10, 10, (i*(360/4)), projectile:speed, false)
                        )
                        if(windDirection <> 0) then(
                            for(i, 0, 3) do(
                                createProjectile(7, sliceCX(npcSl), sliceCY(npcSl), 10, 10, (i*(360/4)) + (360/8), projectile:speed, false)
                            )
                        )
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        setEnemyActDelay(ref, 24)
                    ) else(
                        setEnemyPatternStep(ref, 0)
                        setEnemyState(ref, ai:idle)
                        setEnemyActDelay(ref, 120)
                    )
                )
                case(ai:charge) do(
                    # teleport to other location
                    if(getSliceVisible(npcSl)) then(
                        newSet(62)
                        setSliceVisible(npcSl, false)
                        setEnemyActDelay(ref, 72)
                        putSlice(npcSl, random(159*20, 169*20), random(17*20, 22*20))
                        putSliceSubpixel(npcSl, sliceX(npcSl), sliceY(npcSl))
                        updateNPCorHeroFromSlice(npcSl)
                        startAOE(sliceCX(npcSl), sliceCY(npcSl), 40, 16, player:animSpeed * 6, 3, 16, 3)
                    ) else(
                        setSliceVisible(npcSl, true)
                        newSet(72)
                        setEnemyActDelay(ref, player:animSpeed*6)
                        setEnemyState(ref, ai:spray)
                    )
                )
                case(ai:mega) do(
                    if(windDirection == 0) then(
                        # Start wind direction
                        windDirection := 1 -- (2 * random(0,1))    # randomly -1 or 1
                    ) else(
                        windDirection := windDirection * -1
                    )
                    # Disable opposite speakers
                    variable(first, last)
                    for(i, 0, 7) do(
                        setEnemyState(globalNPC(enemy:speaker, i), ai:idle)
                    )
                    if(windDirection > 0) then(
                        first := 0
                        last := 3
                    ) else(
                        first := 4
                        last := 7
                    )
                    for(i, first, last) do(
                        setEnemyState(globalNPC(enemy:speaker, i), ai:charge)
                    )
                    newSet(62)
                    setEnemyActDelay(ref, 400)
                    setEnemyState(ref, ai:idle)
                )
            )
        )
        case(enemy:speaker) do(
            if(getEnemyState(ref) == ai:wait || getEnemyState(ref) == ai:charge) then(break)

            variable(projectile, offset)

            if(NPCDirection(ref) == left) then(
                bearing := 270
                offset := -20
            ) else(
                bearing := 90
                offset := 20
            )

            if(getAttackDelay(npcSl) == 0) then(
                projectile := createProjectile(6, sliceCX(npcSl)+offset, sliceCY(npcSl), 8, 18, bearing, projectile:speed, false)
                if(NPCDirection(ref) == left) then(horizFlipSprite(firstSpriteChild(projectile), true))
                setAttackDelay(npcSl, 120)
            )
        )
    )

    subscript, telegraphFX, begin
        variable(vfx)
        vfx := loadHeroSprite(4)
        centerSlice(vfx)
        setParent(vfx, fxLayer)
        putSlice(vfx, sliceCX(npcSl), sliceCY(npcSl))
        createAnimation(vfx, 40, 5, false, dissolve:puff)
        playSound(sfx:Beast4, false, true)
    end

    subscript, newSet, set, begin
        variable(sprite)
        sprite := getNPCSprite(ref)
        setSliceExtra(firstChild(sprite), extra0, set)       # change origin set
        setSpriteSetNumber(sprite, set)
        setSliceExtra(sprite, extra0, 0)     # Reset animation timer
    end
end

script, getEnemyActDelay, ref, begin
    exitReturning(npcExtra(ref, extra0))
end

script, setEnemyActDelay, ref, v, begin
    setNPCExtra(ref, extra0, v) 
end

script, getEnemyHit, ref, begin
    exitReturning(npcExtra(ref, extra1))
end

script, setEnemyHit, ref, b, begin
    setNPCExtra(ref, extra1, b)
end

script, stunEnemy, ref, dur, begin
    switch(getNPCID(ref)) do(
        case(enemy:testboss) exitReturning(false)
        case(enemy:charvile) exitReturning(false)
        case(enemy:chadbile) exitReturning(false)
        case(enemy:starbile) exitReturning(false)
        case(enemy:speaker) exitReturning(false)
    )
    setNPCExtra(ref, extra2, dur)
end

script, getEnemyStun, ref, begin
    exitReturning(npcExtra(ref, extra2))
end

script, getEnemyState, ref, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    exitReturning(getSliceExtra(sl, extra0))
end

script, setEnemyState, ref, v, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    setSliceExtra(sl, extra0, v)
end

script, getEnemyPatternStep, ref, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    exitReturning(getSliceExtra(sl, extra1))
end

script, setEnemyPatternStep, ref, v, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    setSliceExtra(sl, extra1, v)
end

script, getEnemySequenceStep, ref, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    exitReturning(getSliceExtra(sl, extra2))
end

script, setEnemySequenceStep, ref, v, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    setSliceExtra(sl, extra2, v)
end

script, setEnemyMelee, ref, v, begin
    variable(sl, spr)

    sl := lookupSlice(sli:attack, getNPCSlice(ref))
    setSliceVisible(sl, v)
    spr := firstChild(sl)
    createAnimation(spr, 32, 4, true)
end

# Attack Patterns
# ===================================

script, startAOE, x, y, w, h, delay=60, dmg=1, set=5, antFrames=0, begin
    variable(sl, fill, sprite)

    sl := createRect(w, h)
    setParent(sl, aoeLayer)
    putSlice(sl, x, y)
    setHorizAnchor(sl, edge:center)
    setVertAnchor(sl, edge:middle)
    setRectOpacity(sl, 10)
    setRectFgcol(sl, 16)
    setRectBgcol(sl, 15)
    setRectBorder(sl, border:line)

    fill := createRect(0, 0)
    setParent(fill, sl)
    centerSlice(fill)
    setRectBorder(fill, border:none)
    setRectBgcol(fill, 15)
    setRectOpacity(fill, 50)
    setSliceExtra(fill, extra0, delay)

    sprite := loadHeroSprite(set)
    setParent(sprite, sl)
    setSliceVisible(sprite, false)
    centerSlice(sprite)

    setSliceExtra(sl, extra0, delay)
    setSliceExtra(sl, extra1, dmg)
    setSliceExtra(sl, extra2, antFrames)
end

script, processAOE, begin
    variable(i, sl, fill, timer, w, h, sprite, x, y)

    for(i, childCount(aoeLayer)--1, 0, -1) do(
        sl := sliceChild(aoeLayer, i)
        timer := getSliceExtra(sl, extra0)
        if(timer <= 0) then(
            if(sliceCollide(sl, player)) then(
                damagePlayer(getSliceExtra(sl, extra1))
            )
            freeSlice(sl)
            continue
        )

        fill := firstRectChild(sl)
        w := ((getSliceExtra(fill, extra0) -- timer) * sliceWidth(sl)) / getSliceExtra(fill, extra0)
        h := ((getSliceExtra(fill, extra0) -- timer) * sliceHeight(sl)) / getSliceExtra(fill, extra0)
        setSliceSize(fill, w, h)

        if(timer == getSliceExtra(sl, extra2)) then(
            sprite := firstSpriteChild(sl)
            x := sliceScreenX(sprite)
            y := sliceScreenY(sprite)
            setParent(sprite, lookupSlice(sl:mapOverlay))
            putSliceScreen(sprite, x, y)
            setSliceVisible(sprite, true)
            createAnimation(sprite, 24, 3)
        )

        setSliceExtra(sl, extra0, timer--1)
    )
end

plotscript, harmTileStepon, size, ref, begin
    if(getEnemyActDelay(ref) == 0) then(
        startAOE(sliceCX(getNPCSlice(ref)), sliceCY(getNPCSlice(ref)), size, size, 30, 1, 10, 1)
        setEnemyActDelay(ref, 40)
    )
end

# Getter functions
# ====================================

script, getEnemyHP, ref, begin
    variable(id, default)

    default := 1

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:melee) exitReturning(4)
        case(enemy:ranged) exitReturning(2)
        case(enemy:testboss) exitReturning(100)
        case(enemy:slime) exitReturning(5)
        case(enemy:charvile) exitReturning(266)
        case(enemy:chadbile) exitReturning(266)
        case(enemy:starbile) exitReturning(266)
        case(enemy:fan) exitReturning(2)
        case(enemy:flame) exitReturning(1)
        case(else) exitReturning(default)
    )
end

script, getEnemyWidth, ref, begin
    variable(id, default)

    default := 12

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:testboss) exitReturning(40)
        case(enemy:slime) exitReturning(18)
        case(enemy:charvile) exitReturning(40)
        case(enemy:chadbile) exitReturning(36)
        case(enemy:flame) exitReturning(16)
        case(enemy:starbile) exitReturning(16)
        case(enemy:speaker) exitReturning(20)
        case(else) exitReturning(default)
    )
end

script, getEnemyHeight, ref, begin
    variable(id, default)

    default := 8
    
    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:testboss) exitReturning(15)
        case(enemy:slime) exitReturning(8)
        case(enemy:charvile) exitReturning(30)
        case(enemy:chadbile) exitReturning(32)
        case(enemy:flame) exitReturning(10)
        case(enemy:starbile) exitReturning(22)
        case(enemy:speaker) exitReturning(20)
        case(else) exitReturning(default)
    )
end

script, getEnemyAttackWidth, ref, begin
    variable(id, default)

    default := 12

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:chadbile) exitReturning(74)
        case(else) exitReturning(default)
    )
end

script, getEnemyAttackHeight, ref, begin
    variable(id, default)

    default := 8
    
    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:chadbile) exitReturning(25)
        case(else) exitReturning(default)
    )
end

script, getEnemyFrames, ref, begin
    variable(id)

    if(getNPCPool(ref) == pool:global) then(
        id := getNPCID(ref)

        switch(id) do(
            case(enemy:dummy) exitReturning(4)
            case(enemy:testboss) exitReturning(1)
            case(enemy:slime) exitReturning(2)
            case(enemy:charvile) exitReturning(1)
            case(enemy:chadbile) exitReturning(2)
            case(enemy:flame) exitReturning(5)
            case(enemy:starbile) exitReturning(6)
            case(enemy:speaker) exitReturning(4)
            case(enemy:stardummy) exitReturning(1)
            case(else) exitReturning(4)
        )
    ) else(
        id := getNPCID(ref)
        switch(id) do(
            case(npc:charmile) exitReturning(4)
            case(23) exitReturning(10)   # minecraft fox
            case(25) exitReturning(6)   # shopbile
            case(else) exitReturning(1)
        )
    )
end

script, getEnemySpeed, ref, begin
    variable(id, default)

    default := 1500

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:slime) exitReturning(250)
        case(enemy:testboss) exitReturning(250)
        case(enemy:chadbile) exitReturning(2000)
        case(else) exitReturning(default)
    )
end