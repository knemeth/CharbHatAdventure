# enemy.hss

# NPC EXTRAS
# extra0 = Action delay
# extra1 = Whether the npc was hit by the player's current melee attack
# extra2 = stun duration

defineConstant, begin
    0,enemy:dummy
    1,enemy:melee
    2,enemy:ranged
    3,enemy:testboss
    4,enemy:charvile
    30,enemy:actDelay

    0,ai:idle
    1,ai:spray 
    2,ai:aoe
    3,ai:melee
    4,ai:mega
    5,ai:wait
end

script, initEnemies, begin
    # loop through npcs and apply default values
    variable(ref)

    ref := nextNPCReference()
    while(ref) do(
        if(getNPCPool(ref) == pool:global) then(
            setSliceExtra(getNPCSprite(ref), extra2, getEnemyHP(ref))
            setEnemyState(ref, ai:wait)
        )
        
        ref := nextNPCReference(ref)
    )
end

script, enemyEachtick, begin
    # loop through npcs and apply behaviors based on id
    variable(ref)

    ref := nextNPCReference()
    while(ref) do(
        if(getNPCPool(ref) == pool:global) then(
            enemyBehavior(ref)
        ) else(
            if(getEnemyActDelay(ref) > 0) then(setEnemyActDelay(ref, getEnemyActDelay(ref) -- 1))
        )
        ref := nextNPCReference(ref)
    )
end

script, enemyBehavior, ref, begin
    # check enemy type and apply behavior based on current conditions
    variable(dist, npcSl, bearing, subX, subY, i)

    npcSl := getNPCSlice(ref)

    if(getEnemyStun(ref) > 0) then(
        stunEnemy(ref, getEnemyStun(ref)--1)
        exitScript()
    )

    if(getAttackDelay(npcSl) > 0) then(
        setAttackDelay(npcSl, getAttackDelay(npcSl)--1)
    )

    if(getEnemyActDelay(ref) > 0) then(
        setEnemyActDelay(ref, getEnemyActDelay(ref)--1)
        # Move if still moving
        if(getSliceV(npcSl) > 0) then(
            bearing := getSliceDir(npcSl)
            subX := get_x_subpixels(getSliceV(npcSl), bearing)
            subY := get_y_subpixels(getSliceV(npcSl), bearing)
            applyMove(npcSl, subX, subY)
            # Direction
            if(bearing > 315) then(setNPCDirection(ref, up))
            elseif(bearing > 225) then(setNPCDirection(ref, left))
            elseif(bearing > 135) then(setNPCDirection(ref, down))
            elseif(bearing > 45) then(setNPCDirection(ref, right))
            else(setNPCDirection(ref, up))
        )
        exitScript()
    )

    switch(getNPCID(ref)) do(
        case(enemy:dummy) do(
            # Do nothing
        )
        case(enemy:melee) do(
            # If player within certain radius, move towards them
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist <= getEnemyAttackHeight(ref)) then(
                # Attack
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, 0)
            ) elseif(dist / 20 < 10 && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Move towards player
                bearing := get_bearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            )
        )
        case(enemy:ranged) do(
            # If player within certain radius, move towards them
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist / 20 < 4  && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Run away
                bearing := get_bearing(sliceCX(player), sliceCY(player), sliceCX(npcSl), sliceCY(npcSl))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) elseif(dist / 20 < 10 && dist / 20 > 6 && getSliceExtra(getNPCSprite(ref), extra1) == 0) then(
                # Move towards player
                bearing := get_bearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setEnemyActDelay(ref, enemy:actDelay)
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            ) 

            if(dist / 20 < 8 && getAttackDelay(npcSl) == 0) then(
                # Fire projectile
                bearing := get_bearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, bearing, projectile:speed, false)
                setAttackDelay(npcSl, enemy:actDelay * 3)
            )
        )
        case(enemy:testboss) do(
            # when player gets in range, start fucking shit up
            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist / 20 < 8) then(
                switch(getEnemyState(ref)) do(
                    case(ai:idle) setEnemyActDelay(ref, enemy:actDelay), setEnemyState(ref, random(1, 4))
                    case(ai:spray) do(
                        if(getEnemyPatternStep(ref) < 16) then(
                            createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        
                        setEnemyActDelay(ref, 12)
                    )
                    case(ai:aoe) do(
                        if(getEnemyPatternStep(ref) < 8) then(
                            startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        setEnemyActDelay(ref, 24)
                    )
                    case(ai:mega) do(
                        if(getEnemyPatternStep(ref) < 32) then(
                            createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                            if(getEnemyPatternStep(ref),mod,4) then(
                                for(i, 0, 7) do(
                                    createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, i*(360/8), multdiv(projectile:speed, 3, 5), false)
                                )
                            ) elseif(getEnemyPatternStep(ref),mod,6) then(
                                startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                            )
                            setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                        ) else(
                            setEnemyState(ref, ai:idle)
                            setEnemyPatternStep(ref, 0)
                        )
                        setEnemyActDelay(ref, 8)
                    )
                    case(ai:melee) do(
                        startAOE(sliceCX(npcSl)--sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                        startAOE(sliceCX(npcSl)+sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                        startAOE(0, sliceCY(npcSl)--sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                        startAOE(0, sliceCY(npcSl)+sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                        setEnemyActDelay(ref, 24)
                        setEnemyState(ref, ai:idle)
                    )
                )
            )
        )
        case(enemy:charvile) do(
            if(getEnemyState(ref) == ai:wait) then(break)   # fight hasn't started yet

            dist := getDistance(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
            if(dist / 20 > 8) then(
                # move toward player
                bearing := get_bearing(sliceCX(npcSl), sliceCY(npcSl), sliceCX(player), sliceCY(player))
                setSliceDir(npcSl, bearing)
                setSliceV(npcSl, getEnemySpeed(ref))
            )
            switch(getEnemyState(ref)) do(
                case(ai:idle) do(
                    if(dist < 40) then(
                        setEnemyActDelay(ref, 12)
                        setEnemyState(ref, ai:melee)
                    ) else(
                        setEnemyActDelay(ref, enemy:actDelay)
                        setEnemyState(ref, random(1, 4))
                    )
                )
                case(ai:spray) do(
                    if(getEnemyPatternStep(ref) < 16) then(
                        createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    
                    setEnemyActDelay(ref, 12)
                )
                case(ai:aoe) do(
                    if(getEnemyPatternStep(ref) < 8) then(
                        startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 24)
                )
                case(ai:mega) do(
                    if(getEnemyPatternStep(ref) < 32) then(
                        createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, getEnemyPatternStep(ref)*(360/16), projectile:speed, false)
                        if(getEnemyPatternStep(ref),mod,4) then(
                            for(i, 0, 7) do(
                                createProjectile(0, sliceCX(npcSl), sliceCY(npcSl), 10, 10, i*(360/8), multdiv(projectile:speed, 3, 5), false)
                            )
                        ) elseif(getEnemyPatternStep(ref),mod,6) then(
                            startAOE(sliceCX(player), sliceCY(player), 50, 50, 60, 2, 6, 6)
                        )
                        setEnemyPatternStep(ref, getEnemyPatternStep(ref)+1)
                    ) else(
                        setEnemyState(ref, ai:idle)
                        setEnemyPatternStep(ref, 0)
                    )
                    setEnemyActDelay(ref, 8)
                )
                case(ai:melee) do(
                    startAOE(sliceCX(npcSl)--sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                    startAOE(sliceCX(npcSl)+sliceWidth(npcSl), 0, 50, 50, 60, 2, 6, 6)
                    startAOE(0, sliceCY(npcSl)--sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                    startAOE(0, sliceCY(npcSl)+sliceHeight(npcSl), 50, 50, 60, 2, 6, 6)
                    setEnemyActDelay(ref, 24)
                    setEnemyState(ref, ai:idle)
                )
            )
        )
    )
end

script, getEnemyActDelay, ref, begin
    exitReturning(npcExtra(ref, extra0))
end

script, setEnemyActDelay, ref, v, begin
    setNPCExtra(ref, extra0, v) 
end

script, getEnemyHit, ref, begin
    exitReturning(npcExtra(ref, extra1))
end

script, setEnemyHit, ref, b, begin
    setNPCExtra(ref, extra1, b)
end

script, stunEnemy, ref, dur, begin
    switch(getNPCID(ref)) do(
        case(enemy:testboss) exitReturning(false)   # bosses immune
    )
    setNPCExtra(ref, extra2, dur)
end

script, getEnemyStun, ref, begin
    exitReturning(npcExtra(ref, extra2))
end

script, getEnemyState, ref, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    exitReturning(getSliceExtra(sl, extra0))
end

script, setEnemyState, ref, v, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    setSliceExtra(sl, extra0, v)
end

script, getEnemyPatternStep, ref, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    exitReturning(getSliceExtra(sl, extra1))
end

script, setEnemyPatternStep, ref, v, begin
    variable(sl)

    sl := lookupSlice(sli:ai, getNPCSlice(ref))
    setSliceExtra(sl, extra1, v)
end

# Attack Patterns
# ===================================

script, startAOE, x, y, w, h, delay=60, dmg=1, set=5, antFrames=0, begin
    variable(sl, fill, sprite)

    sl := createRect(w, h)
    setParent(sl, aoeLayer)
    putSlice(sl, x, y)
    setHorizAnchor(sl, edge:center)
    setVertAnchor(sl, edge:middle)
    setRectOpacity(sl, 10)
    setRectFgcol(sl, 19)
    setRectBgcol(sl, 15)
    setRectBorder(sl, border:line)

    fill := createRect(0, 0)
    setParent(fill, sl)
    centerSlice(fill)
    setRectBorder(fill, border:none)
    setRectBgcol(fill, 15)
    setRectOpacity(fill, 50)
    setSliceExtra(fill, extra0, delay)

    sprite := loadHeroSprite(set)
    setParent(sprite, sl)
    setSliceVisible(sprite, false)
    centerSlice(sprite)

    setSliceExtra(sl, extra0, delay)
    setSliceExtra(sl, extra1, dmg)
    setSliceExtra(sl, extra2, antFrames)
end

script, processAOE, begin
    variable(i, sl, fill, timer, w, h, sprite, x, y)

    for(i, childCount(aoeLayer)--1, 0, -1) do(
        sl := sliceChild(aoeLayer, i)
        timer := getSliceExtra(sl, extra0)
        if(timer <= 0) then(
            if(sliceCollide(sl, player)) then(
                damagePlayer(getSliceExtra(sl, extra1))
            )
            freeSlice(sl)
            continue
        )

        fill := firstRectChild(sl)
        w := ((getSliceExtra(fill, extra0) -- timer) * sliceWidth(sl)) / getSliceExtra(fill, extra0)
        h := ((getSliceExtra(fill, extra0) -- timer) * sliceHeight(sl)) / getSliceExtra(fill, extra0)
        setSliceSize(fill, w, h)

        if(timer == getSliceExtra(sl, extra2)) then(
            sprite := firstSpriteChild(sl)
            x := sliceScreenX(sprite)
            y := sliceScreenY(sprite)
            setParent(sprite, lookupSlice(sl:mapOverlay))
            putSliceScreen(sprite, x, y)
            setSliceVisible(sprite, true)
            createAnimation(sprite, 24, 3)
        )

        setSliceExtra(sl, extra0, timer--1)
    )
end

plotscript, harmTileStepon, size, ref, begin
    traceValue(size, ref)
    if(getEnemyActDelay(ref) == 0) then(
        startAOE(sliceCX(getNPCSlice(ref)), sliceCY(getNPCSlice(ref)), size, size, 30, 1, 5, 1)
        setEnemyActDelay(ref, 40)
    )
end

# Getter functions
# ====================================

script, getEnemyHP, ref, begin
    variable(id, default)

    default := 1

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:melee) exitReturning(4)
        case(enemy:ranged) exitReturning(2)
        case(enemy:testboss) exitReturning(100)
        case(else) exitReturning(default)
    )
end

script, getEnemyWidth, ref, begin
    variable(id, default)

    default := 12

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:testboss) exitReturning(40)
        case(else) exitReturning(default)
    )
end

script, getEnemyHeight, ref, begin
    variable(id, default)

    default := 8
    
    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:testboss) exitReturning(15)
        case(else) exitReturning(default)
    )
end

script, getEnemyAttackWidth, ref, begin
    variable(id, default)

    default := 12

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(else) exitReturning(default)
    )
end

script, getEnemyAttackHeight, ref, begin
    variable(id, default)

    default := 8
    
    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(else) exitReturning(default)
    )
end

script, getEnemyFrames, ref, begin
    variable(id)

    if(getNPCPool(ref) == pool:global) then(
        id := getNPCID(ref)

        switch(id) do(
            case(enemy:dummy) exitReturning(4)
            case(enemy:testboss) exitReturning(1)
            case(enemy:charvile) exitReturning(1)
            case(else) exitReturning(4)
        )
    ) else(
        exitReturning(1)
    )
end

script, getEnemySpeed, ref, begin
    variable(id, default)

    default := 1500

    if(getNPCPool(ref) == pool:local) then(exitReturning(default))

    id := getNPCID(ref)

    switch(id) do(
        case(enemy:dummy) exitReturning(10)
        case(enemy:testboss) exitReturning(500)
        case(else) exitReturning(default)
    )
end